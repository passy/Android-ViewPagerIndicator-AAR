apply plugin: 'com.android.library'
apply plugin: 'checkstyle'

android {
    compileSdkVersion 20
    buildToolsVersion '20.0.0'

    defaultConfig {
        minSdkVersion 4
        targetSdkVersion 20
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:20.0.0'
}

checkstyle {
    configFile project.file('../checkstyle.xml')
    showViolations true
}
apply from: 'https://raw.githubusercontent.com/robbypond/gradle-mvn-push/b2ed833b2fbd9c42bb89ca089b75e29b653084ad/gradle-mvn-push.gradle'

import org.gradle.plugins.signing.Sign

gradle.taskGraph.whenReady { taskGraph ->
  if (taskGraph.allTasks.any { it instanceof Sign } &&
      ':library:uploadArchives' in gradle.startParameter.taskNames) {
    // Use Java 6's console to read from the console (no good for a CI environment)
    Console console = System.console()
    console.printf "\n\nWe have to sign some things in this build.\n\nPlease enter your signing details.\n\n"
    allprojects { ext."signing.password" = console.readPassword("PGP Private Key Password: ") }
  }
}